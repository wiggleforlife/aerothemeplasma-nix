perSystem:
{ config, lib, pkgs, ... }:
let
  cfg = config.programs.aeroshell;
  pvcfg = if cfg.aerothemeplasma.plymouth.enable then cfg.aerothemeplasma.plymouth else cfg.vistathemeplasma.plymouth;
  atpkgs = perSystem.config.packages;
  withSessions = list: lib.concatMap (pkg:
    lib.optional cfg.sessions.wayland.enable (pkg.override { session = "wayland"; })
    ++ lib.optional cfg.sessions.x11.enable (pkg.override { session = "x11"; })
  ) list;
in
{
  options.programs = {
    aeroshell = rec {
      enable = lib.mkEnableOption "AeroShell";
      polkit.enable = lib.mkEnableOption "the AeroShell Polkit agent replacement";
      fonts = {
        enable = lib.mkEnableOption "the Segoe UI and Lucida Console fonts";
        segoe.enable = lib.mkEnableOption "the Segoe UI font";
        lucida.enable = lib.mkEnableOption "the Lucida Console font";
      };
      sessions = {
        wayland.enable = lib.mkEnableOption "the Wayland session" // { default = true; };
        x11.enable = lib.mkEnableOption "the X11 session" // { default = config.services.xserver.enable; };
      };
      aerothemeplasma = {
        enable = lib.mkEnableOption "AeroThemePlasma, a set of Plasma theme packages";
        plymouth.enable = lib.mkEnableOption "the PlymouthVista theme using the 7 style";
        plymouth.settings = lib.mkOption {
          type = lib.types.submodule {
            freeformType = with lib.types; attrsOf (oneOf [ str bool int ]);
            options.BootSlowdown = lib.mkOption {
              type = lib.types.int;
              default = 0;
              description = "Minimum duration of the PlymouthVista animation in seconds.";
            };
          };
          description = "plymouth settings"
        };
        sddm.enable = lib.mkEnableOption "the SDDM theme";
      };
      vistathemeplasma = {
        plymouth.enable = lib.mkEnableOption "the PlymouthVista theme using the Vista style";
        plymouth.settings = aerothemeplasma.plymouth.settings;
      };
    };

    linver.enable = lib.mkEnableOption "the Linver application";
    execbin.enable = lib.mkEnableOption "the ExecBin application";
  };

  config = lib.mkIf cfg.enable {
    assertions = [
      {
        assertion = cfg.aerothemeplasma.plymouth.enable -> cfg.fonts.segoe.enable;
        message = ''
          The Plymouth theme requires the Segoe font to be enabled.
          Like so: "programs.aeroshell.fonts.segoe.enable = true;"
        '';
      }
      {
        assertion = cfg.sessions.wayland.enable || cfg.sessions.x11.enable;
        message = ''
          Both sessions under programs.aeroshell.sessions are disabled. How did that happen?
          Please enable one like so: programs.aeroshell.sessions.<wayland/x11>.enable = true;
        '';
      }
      {
        assertion = cfg.sessions.x11.enable -> config.services.xserver.enable;
        message = ''
          The X11 session requires the X server to be enabled.
          Enable it like so: "services.xserver.enable = true;"
        '';
      }
      {
        assertion = !(cfg.aerothemeplasma.plymouth.enable && cfg.vistathemeplasma.plymouth.enable);
        message = "Both Plymouth styles under programs.aeroshell are enabled. Choose one.";
      }
    ];

    services.displayManager.sessionPackages = lib.mkIf cfg.aerothemeplasma.enable (withSessions [ atpkgs.login-session ]);
    
    environment.systemPackages = with atpkgs; [
      pkgs.kdePackages.qtmultimedia libplasma plasma-workspace

      dimscreenaero fadingpopupsaero flip3d i18n-kwin loginaero smod 
      smodpeekeffect smodpeekscript squashaero thumbnail-aero thumbnails

      kcmloader libaeroshellutils libshowdesktop libtaskmanager
    ] ++ withSessions (with atpkgs; [
      aeroglassblur aeroglide launchfeedback smodglow smodsnap
    ]) ++ (with atpkgs; lib.optionals cfg.aerothemeplasma.enable [
      cursors icons sounds

      atpootb authui7 color-scheme kvantum-windows7aero
      layout-template seven-black shell

      battery desktopcontainment digitalclocklite keyboardlayout
      networkmanagement notifications panel sevenstart seventasks
      systemtray volume win7showdesktop

      pkgs.kdePackages.qtstyleplugin-kvantum
    ]) 
      ++ lib.optionals cfg.aerothemeplasma.sddm.enable [ atpkgs.sddm-theme-mod ]
      ++ lib.optionals config.programs.linver.enable [ atpkgs.linver ]
      ++ lib.optionals config.programs.execbin.enable [ atpkgs.execbin ];

    # backward compat for users of "programs.aeroshell.fonts.enable"
    programs.aeroshell.fonts = lib.mkIf cfg.fonts.enable {
      segoe.enable = lib.mkDefault true;
      lucida.enable = lib.mkDefault true;
    };

    fonts.packages = lib.optionals cfg.fonts.segoe.enable [ atpkgs.segoe-ui ] 
      ++ lib.optionals cfg.fonts.lucida.enable [ atpkgs.lucida-console ];
    
    systemd.packages = with atpkgs; lib.optionals cfg.polkit.enable [
      uac-polkit-agent
    ];

    boot.plymouth = lib.mkIf pvcfg.enable {
      theme = "PlymouthVista";
      themePackages = [( atpkgs.plymouthvista.override { settings = pvcfg.settings; } )];
    };
    # https://github.com/furkrn/PlymouthVista/blob/cc6592a29387462d003c2c95cb9cb5df3fea851f/systemd/slowdown/plymouth-vista-slow-boot-animation.service
    # https://wiki.archlinux.org/title/Plymouth#Slow_down_boot_to_show_the_full_animation
    systemd.services.plymouth-vista-slow-boot-animation = lib.mkIf (pvcfg.settings.BootSlowdown > 0) {
      description = "Waits for Plymouth animation to finish";
      wantedBy = [ "multi-user.target" ];
      after = [ "plymouth-start.service" ];
      before = [ "plymouth-quit.service" ];
      serviceConfig = {
        Type = "oneshot";
        ExecStart = "${lib.getExe' pkgs.coreutils "sleep"} ${lib.toString pvcfg.settings.BootSlowdown}";
      };
    };
    programs.aeroshell.aerothemeplasma.plymouth.settings = {
      AuthuiStyle = "7";
      UseLegacyBootScreen = false;
      UseShadow = lib.mkDefault true;
    };

    services.displayManager.sddm = lib.mkIf cfg.aerothemeplasma.sddm.enable {
      theme = "sddm-theme-mod";
      extraPackages = [ pkgs.kdePackages.kitemmodels ];
      settings = {
        Theme = {
          CursorTheme = "aero-drop";
          CursorSize = 30;
          Font = lib.mkIf cfg.fonts.segoe.enable "Segoe UI";
        };
      };
    };
  };
}
